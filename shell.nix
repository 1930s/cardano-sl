{ system ? builtins.currentSystem
, config ? {}
, iohkPkgs ? import ./. {inherit config system; }
, pkgs ? iohkPkgs.pkgs
}:
with pkgs;
let
  localLib = import ./lib.nix;

  # Filters a haskell package set and returns only the packages which
  # are (transitive) dependencies of the cardano-sl packages.
  getCardanoSLDeps = with lib; let
    notCardano = drv: !(localLib.isCardanoSL drv.name);
    isTopLevel = name: drv: localLib.isCardanoSL name && (drv ? "override");
    getTopLevelDrvs = ps: attrValues (filterAttrs isTopLevel ps);
    sorted = ps: attrValues (listToAttrs (map (drv: { name = drv.pname; value = drv; }) ps));
  in
    ps: sorted (filter notCardano (concatMap haskell.lib.getHaskellBuildInputs (getTopLevelDrvs ps)));

  ghc = iohkPkgs.ghc.withPackages getCardanoSLDeps;

  stackDeps = [
    zlib openssh autoreconfHook openssl
    gmp rocksdb git bsdiff ncurses lzma
    perl bash
  ];
  # TODO: add cabal-install (2.0.0.1 won't work)
  devTools = [ hlint iohkPkgs.haskellPackages.stylish-haskell ];

  cardanoSL = haskell.lib.buildStackProject {
    inherit (haskell.packages.ghc822) ghc;
    name = "cardano-sl-env";

    buildInputs = devTools ++ stackDeps
      # cabal-install and stack pull in lots of dependencies on OSX so skip them
      # See https://github.com/NixOS/nixpkgs/issues/21200
      ++ (lib.optionals stdenv.isLinux [ stack ])
      ++ (lib.optionals stdenv.isDarwin (with darwin.apple_sdk.frameworks; [ Cocoa CoreServices libcxx libiconv ]));

    phases = ["nobuildPhase"];
    nobuildPhase = "mkdir -p $out";
  };

  forCabal = mkShell {
    name = "cardano-sl-env";
    buildInputs = [ ghc ] ++ devTools;
    shellHook = lib.optionalString lib.inNixShell ''
      eval "$(egrep ^export ${ghc}/bin/ghc)"
    '';
  };

  fixStylishHaskell = stdenv.mkDerivation {
    name = "fix-stylish-haskell";
    buildInputs = [ iohkPkgs.haskellPackages.stylish-haskell git ];
    shellHook = ''
      git diff > pre-stylish.diff
      find . -type f -not -path '.git' -not -path '*.stack-work*' -name "*.hs" -not -name 'HLint.hs' -exec stylish-haskell -i {} \;
      git diff > post-stylish.diff
      diff pre-stylish.diff post-stylish.diff > /dev/null
      if [ $? != 0 ]
      then
        echo "Changes by stylish have been made. Please commit them."
      else
        echo "No stylish changes were made."
      fi
      rm pre-stylish.diff post-stylish.diff
      exit
    '';
  };

  # Writes out a cabal new-freeze file containing the exact same
  # dependency versions as are provided by this shell.
  cabalProjectFreeze = let
    contents = ''
      -- This file is automatically generated from stack.yaml.
      constraints: ${constraints}
    '';
    constraints = lib.concatMapStringsSep ",\n             "
      makeConstraint (getCardanoSLDeps iohkPkgs.haskellPackages);
    makeConstraint = dep: "${dep.pname} ==${dep.version}";
  in
    pkgs.writeText "cabal.project.freeze" contents;

in cardanoSL // {
  inherit fixStylishHaskell cabalProjectFreeze forCabal;
}
